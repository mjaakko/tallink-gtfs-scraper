import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.2'
}

group = 'xyz.malkki'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/mjaakko/gtfs-library")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    implementation platform('com.fasterxml.jackson:jackson-bom:2.16.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'commons-cli:commons-cli:1.6.0'

    implementation "xyz.malkki:gtfs-library:0.2.2"

    implementation 'de.grundid.opendatalab:geojson-jackson:1.14'
}

application {
    mainClassName = "xyz.malkki.tallinkgtfsscraper.MainKt"
}

jib {
    container {
        mainClass = mainClassName
    }
    from {
        platforms {
            platform {
                architecture = "amd64"
                os = "linux"
            }
            platform {
                architecture = "arm64"
                os = "linux"
            }
        }
    }
    to {
        image = "mjaakko/tallink-gtfs-scraper:edge"
    }
}

test {
    useJUnitPlatform()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}

tasks.withType(KotlinCompilationTask.class).configureEach {
    compilerOptions {
        freeCompilerArgs.add("-opt-in=kotlin.time.ExperimentalTime")
    }
}